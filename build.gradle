plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
  	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = modMinecraftVersion + "-" +modVersion +"-"+modLoader
group = modGroup // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = modFilename

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
println('Mod Name: ' + modFilename + ', Mod Version: ' + modVersion + ', Minecraft Version: ' + modMinecraftVersion + ', '+modLoader+' Version: ' + modLoaderVersion)

loom {
    accessWidenerPath = file("src/main/resources/"+modId+".accesswidener")
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run/Fabric Client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run/Fabric Server")
        }
    }
}

repositories {
	maven {url = "https://repo.spongepowered.org/maven"} // mixin
	maven {url "https://www.cursemaven.com"} //cursemaven
	maven {url "https://modmaven.dev/" } //UnionLib
	maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
    }
    maven {
        name = "ARRP"
        url = uri("https://ueaj.dev/maven")
        content {
            includeGroup "net.devtech"
        }
    }
    
    maven {
        url "https://maven.siphalor.de/"
        name "Siphalor's Maven (Carry On)"
    }
    maven {url 'https://maven.blamejared.com/'}
    maven {url 'https://jitpack.io'}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.modMinecraftVersion}"
	mappings loom.layered() {
  		officialMojangMappings()
  		parchment("org.parchmentmc.data:"+mappingsChannel+"-"+mappingsVersion+"@zip")
	}
	modImplementation "com.stereowalker.unionlib:UnionLib:"+modMinecraftVersion+"-"+unionLibVersion+"-"+modLoader+""
	modImplementation "net.fabricmc:fabric-loader:${project.modFabricLoaderVersion}"
	modImplementation "net.fabricmc:fabric-language-kotlin:1.9.0+kotlin.1.8.0"
	modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${forgeConfigApiVersion}"
	
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.0.1"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.0.1"
    modImplementation "net.devtech:arrp:0.6.7"
	modImplementation "curse.maven:carrier-409184:4605884"
	
	implementation "com.github.llamalad7.mixinextras:mixinextras-fabric:0.2.0-beta.6"
	modImplementation "tschipp.carryon:carryon-fabric-1.20.1:2.1.2.5"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.modLoaderVersion}+${project.modMinecraftVersion}"
	implementation 'com.electronwill.night-config:core:3.6.3'
	implementation 'com.electronwill.night-config:toml:3.6.3'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

curseforge {
    project {
    	id = modCurseId
		apiKey = "$System.env.CURSEFORGEKEY"
		changelog = file('changelog.md')
		releaseType = 'release'
		addGameVersion(modMinecraftVersion)
		addGameVersion("1.20")
        addGameVersion("${project.modLoader}")
        relations {
        	requiredDependency 'unionlib'
        	optionalDependency 'carrier'
        	optionalDependency 'carry-on'
        }
		mainArtifact(remapJar) {
			displayName = modFilename+"-${project.version}"
		}
		afterEvaluate {
			uploadTask.dependsOn(remapJar)
		}
	}
	options {
    	debug = false // defaults to false
    	forgeGradleIntegration = false
  	}	
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = modId
    versionNumber = "${project.version}"
    versionType = "release"
    uploadFile = remapJar
    gameVersions = [modMinecraftVersion, "1.20"]
    loaders = ["${project.modLoader}".toLowerCase()]
	changelog = file('changelog.md').text
    dependencies {
        required.project "unionlib"
    }
}

publishing {
	tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
			artifact project.file("${project.buildDir}/libs/"+modFilename+"-${project.version}.jar")
        
        	groupId = project.group
			artifactId = project.archivesBaseName
			version = project.version
        }
    }
    repositories {
        maven {
            name = "modmaven"
			credentials {
				username "$System.env.ARTIFACTORYUSER"
				password "$System.env.ARTIFACTORYPASS"
			}
			url = "https://modmaven.dev/artifactory/local-releases/"
        }
    }
}
